\documentclass[12pt,a4paper,twoside]{article}
\usepackage[utf8]{inputenc}
\usepackage{amsmath}
\usepackage{lmodern}
\usepackage{textcomp}
\usepackage{amsfonts}
\usepackage{amssymb}
\usepackage{graphicx}
\usepackage[left=2cm,right=2cm,top=2cm,bottom=2cm]{geometry}
\author{Carlos Eduardo Martínez Núñez}
% used in maketitle                                                             
\title{\textbf{Movimiento de Proyectiles con Resistencia del Aire}}
\begin{document}
\maketitle
Para el estudio de proyectiles con resistencia del aire(figura 1), consideremos un proyectil de masa m, lanzado a un tiempo $t_{0}=0$ al nivel de la superficie terrestre con un ángulo $\theta$ y una velocidad inicial $v_{0}$. Adicional a la fuerza por la gravedad, se considera la presencia de una fuerza por la resistencia del aire, opuesta a la dirección del movimiento, proporcional a la velocidad instantánea.
\begin{figure}[htbp]
\centering
\includegraphics[width=12cm]{friccionAire.png}
\caption{Trayectoria de un proyectil con resistencia del aire.}\label{fig:figura1}
\end{figure}

Considerando el sistema de coordenadas cartesianas y que el movimiento se realiza completamente en el plano x-y, la ecuación del movimiento, viene dada por la siguiente ecuación:
\begin{eqnarray}
m\frac{dv}{dt}=mg-cv
\end{eqnarray}
En donde $v=(v_{x},v_{y})$ es la velocidad del proyectil, $g=(0,-g)$ es la aceleración gravitacional y c es una constante positiva. Las ecuaciones que describen el movimiento por componentes, esta dada por:
\begin{eqnarray}
m\frac{dv_{x}}{dt}=-cv_{x}\nonumber\\
m\frac{dv_{y}}{dt}=-mg-cv_{y}
\end{eqnarray}
 Las ecuaciones anteriores, pueden resolverse usando métodos numéricos,
como el método de integración de Euler. Este método consiste en solucionar una ecuación diferencial ordinaria de primer orden, através de un procedimiento numérico de primer orden, partiendo de unos valores iniciales. 
En general, si se conoce el valor de una funcion $y(t)$, para un $t_{0}$ dado, el siguiente valor de la función para $t_{0}+h$, tomando un h lo sufientemente pequeño, puede  determinarse, empleando una expansión de Taylor alrededor de $t_{0}$, como sigue:
\begin{eqnarray}
f(t_{0}+h)=f(t_{0})+hy'(t_{0})+\frac{1}{2}h^{2}y''(t_{0})+O(h^{3})
\end{eqnarray}
Despreciando los terminos cuadráticos y mayores de h, obtenemos:
\begin{eqnarray}
y'(t_{0})\simeq\frac{f(t_{0})-f(t_{0}+h)}{h}
\end{eqnarray}
Tomando $y'(t_{0})=f(t,y(t))$, $y(t_{0})=y_{0}$ y $t_{n}=t_{0}+nh$, el siguiente paso de $t_{n}$ a $t_{n+1}$, para $y(t)$según el método de Euler, corresponde a:
\begin{eqnarray}
y_{n+1}=y_{n}+hf(t_{n},y_{n})
\end{eqnarray}
\section{Aplicación del Método de Euler al Movimiento de Proyectiles con Resistencia de Aire}
\subsection{Componente Horizontal de la Velocidad}
Considerando la ecuación diferencial de la componente horizontal de la velocidad, tenemos:
\begin{eqnarray}
\frac{dv_{x}}{dt}=-\frac{c}{m}v_{x}=f(t,v_{x}(t))
\end{eqnarray}
Según el método de Euler el valor de $v_{x}(t_{n+1})$ a partir de $t_{n}$, corresponde a:
\begin{eqnarray}
v_{x}(t_{n+1}))=v_{x}(t_{n})-\frac{\delta t}{m}cv_{x}(t_{n})\nonumber\\
v_{x}(t_{n+1}))=v_{x}(t_{n})[1-\frac{\delta t}{m}c]
\end{eqnarray}
Donde $h=\delta t$. La componente x de la posición se determinar a partir de:
\begin{eqnarray}
\frac{dv_{x}}{dt}=v_{x}\simeq \frac{x_{n+1}-x_{n}}{\delta t}
\end{eqnarray}
Luego:
\begin{eqnarray}
x_{n+1}=x_{n}+{\delta t}v_{x}(t_{n})
\end{eqnarray}
\subsection{Componente Vertical de la Velocidad}
Similarmente al proceso realizado para $v_{x}$, tenemos:
\begin{eqnarray}
\frac{dv_{y}}{dt}=-g-\frac{c}{m}v_{y}=f(t,v_{y}(t))\\
v_{y}(t_{n+1}))=v_{y}(t_{n})[1-\frac{\delta t}{m}c]-g\delta t\\
y_{n+1}=y_{n}+{\delta t}v_{y}(t_{n})-g\delta t
\end{eqnarray}
\section{Aplicación Fortran para la Trayectoria de un Proyectil con Resistencia del Aire usando el Método de Euler}
El código de la aplicación fortran para determinar las trayectorias de un proyectil esférico con resistencia del aire y velocidades $v_{0}$ entre 2 y 10 m/s, con valores iniciales $t_{0}=0$, $y_{0}=0$ y $x_{0}=0$, usando el método de integración de Euler.
El código Fortran corresponde a:
\begin{verbatim}
program resistenciaAire
  implicit none

   !**************************************************************************************************
   !  Programa para calcular la trayectoria de un proyectil esférico con resistencia del aire        *
   !                                                                                                 *
   !     m -------------- masa del proyectil                                                         *
   !     r -------------- radio del proyectil                                                        *
   !     v0 ------------- velocidad de lanzamiento                                                   *
   !     vt-------------- velocidad terminal                                                         *
   !     cd-------------- coeficiente de arrastre                                                    *
   !     rho_a----------- densidad de aire                                                           *
   !     a--------------- ángulo de lazamiento en grados                                             *
   !     dt-------------- incremento del tiempo                                                      *
   !     g--------------- aceleración gravitacional                                                  *
   ! *************************************************************************************************
   
  !Definición de parámetros y variables
  
  real, parameter:: g=9.8, pi=3.1415927, rho_a=1.128, cd =0.45, dt=0.01
  real, parameter:: m=0.5, theta = 45., r = 0.5
  integer, parameter:: size=1000
  integer::i,j
  real::vt,a,C,v0
  real, dimension(0:size) ::t,v_x,v_y,x,y
  
  !**************************************************************************************************
  !**************************************************************************************************
  !Calculo de la velocidad terminal
  
  vt= sqrt((2*m*g)/(rho_a*pi*r**2*cd))

  !Calculo de la constante positiva C
  C = m*g / vt
   
  ! convirtiendo ángulo a radianes
  a = theta * pi / 180.0
  !Condiciones iniciales
  !***************************************************************************************************
  !***************************************************************************************************
  !Defición de Loops

  open(1, file='friccion.dat', status='unknown')  
   
    do j=2,10,2
   
  !*************************************************************************************************
  !Condiciones Iniciales 
   v0=real(j)
     t(0)=0.
     x(0)=0.
     y(0)=0.
     v_x(0)=v0*cos(a)
     v_y(0)=v0*sin(a) 
    write(1,1000) t(0), x(0), y(0), v_x(0), v_y(0)  
 
  !Cálculo del primer punto despreciando la fricción
     
     t(1)= t(0)+dt   
     x(1) = x(0) + v0*t(1)*cos(a)
     y(1) = y(0) + v0*t(1)*sin(a)-0.5*g*t(1)**2
     v_x(1) = v0*cos(a)
     v_y(1) = v0*sin(a)-g*t(1)
     write(1,1000) t(1), x(1), y(1),v_x(1),v_y(1)
  !************************************************************************************************
  !Cálculos considerando la fricción del aire
    
   do i=2,size

     t(i) = t(i-1) + dt   
     x(i) = x(i-1) + dt*v_x(i-1)
     y(i) = y(i-1) + dt*v_y(i-1)
     v_x(i)= v_x(i-1)*(1-(dt*C)/m)
     v_y(i)= v_y(i-1)*(1-(dt*C)/m)-dt*g
    
     if (y(i)<0.) exit 
      
     write(1,1000) t(i), x(i), y(i),v_x(i),v_y(i)
     1000 format(f18.15,5x,f18.15, 5x, f18.15, 5x, f18.15,5x,f18.15) 
     end do 
     write(1,1100)
     1100 format(/)
   !*********************************************************************************************** 
   !Vaciando las variables para el cálculo con el siguiente valor de la velocidad inicial 
     do i=2,size
     t(i)=0.
     x(i)=0.
     y(i)=0.
     v_x(i)=0.
     v_y(i)=0.
     end do

     end do 
     close(1)
  
end program resistenciaAire
\end{verbatim}
El Scritp para la graficación de los datos de salida usando Gnuplot corresponde a:
\begin{verbatim}
set title "Trayectoria Movimiento de Protectiles con Resistencia del Aire"
set title font ",15" norotate
set xlabel "Alcance Horizontal"
set xlabel font "Verdana,12"
set ylabel "Alcance Vertical"
set ylabel font "Verdana,12"
set style data points
set xrange [0:3.5]
set yrange [0:1.5]
set pointsize 0.4
plot "friccion.dat" index 0 using 2:3  with linespoints ls 5 title "Vo = 2",\
"friccion.dat" index 1 using 2:3  with linespoints ls 6 title "Vo = 4",\
 "friccion.dat" index 2 using 2:3  with linespoints ls 7 title "Vo = 6",\
"friccion.dat" index 3 using 2:3  with linespoints ls 8 title "Vo = 8",\
 "friccion.dat" index 4 using 2:3  with linespoints ls 9 title "Vo = 10"
\end{verbatim}
\end{document}